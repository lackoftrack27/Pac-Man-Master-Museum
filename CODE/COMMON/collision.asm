/*
------------------------------------------------
        COLLISION RELEATED FUNCTIONS
------------------------------------------------
*/

/*
    INFO: CHECKS IF PAC-MAN INTERSECTS WITH ANY OF THE 4 GHOSTS (USING TILE POSITIONS)
    INPUT: NONE
    OUTPUT: NONE
    USES:  AF, BC, DE, HL, IX
*/
globalCollCheckTile:
;   ACTOR POINTER SETUP
    LD IX, clyde
    LD BC, -_sizeof_ghost
;   CLYDE
    ; CHECK IF GHOST IS ALIVE
    LD A, (clyde + ALIVE_FLAG)
    OR A
    JR Z, + ; IF NOT, CHECK NEXT GHOST
    ; CHECK IF PAC-MAN AND GHOST ARE AT SAME TILE
    LD HL, (pacman + CURR_X)
    LD DE, (clyde + CURR_X)
    SBC HL, DE
    JR Z, interactDetermination ; IF SO, CHECK GHOST'S STATE TO DETERMINE NEXT ACTION
;   INKY
+:
    ADD IX, BC  ; GHOST POINTER = INKY
    ; CHECK IF GHOST IS ALIVE
    LD A, (inky + ALIVE_FLAG)
    OR A
    JR Z, + ; IF NOT, CHECK NEXT GHOST
    ; CHECK IF PAC-MAN AND GHOST ARE AT SAME TILE
    LD HL, (pacman + CURR_X)
    LD DE, (inky + CURR_X)
    SBC HL, DE
    JR Z, interactDetermination ; IF SO, CHECK GHOST'S STATE TO DETERMINE NEXT ACTION
;   PINKY
+:
    ADD IX, BC  ; GHOST POINTER = PINKY
    ; CHECK IF GHOST IS ALIVE
    LD A, (pinky + ALIVE_FLAG)
    OR A
    JR Z, + ; IF NOT, CHECK NEXT GHOST
    ; CHECK IF PAC-MAN AND GHOST ARE AT SAME TILE
    LD HL, (pacman + CURR_X)
    LD DE, (pinky + CURR_X)
    SBC HL, DE
    JR Z, interactDetermination ; IF SO, CHECK GHOST'S STATE TO DETERMINE NEXT ACTION
;   BLINKY
+:
    ADD IX, BC  ; GHOST POINTER = BLINKY
    ; CHECK IF GHOST IS ALIVE
    LD A, (blinky + ALIVE_FLAG)
    OR A
    RET Z   ; IF NOT, END
    ; CHECK IF PAC-MAN AND GHOST ARE AT SAME TILE
    LD HL, (pacman + CURR_X)
    LD DE, (blinky + CURR_X)
    SBC HL, DE
    JR Z, interactDetermination ; IF SO, CHECK GHOST'S STATE TO DETERMINE NEXT ACTION
    RET




/*
    INFO: DETERMINES WHAT TO DO WHEN PAC-MAN HAS COLLIDED WITH A GHOST
    INPUT: NONE
    OUTPUT: NONE
    USES:  AF, HL, IX
*/
interactDetermination:
;   SET GHOST POINTS SPR_NUM TO GHOST
    LD A, (IX + SPR_NUM)
    LD (ghostPointSprNum), A
;   CHECK IF GHOST IS EDIBLE
    BIT 0, (IX + EDIBLE_FLAG)
    JR Z, @ghostKill    ; IF NOT, GHOST WILL KILL PAC-MAN
@ghostEaten:
;   PAC-MAN IS EATING GHOST
    ; SWITCH TO EAT MODE
    LD A, $01
    LD (eatSubState), A
    ; HIDE GHOST (DOESN'T ACTUALLY DO THAT, BUT SIGNALS TO SPRITE FLICKER CODE)
    LD (IX + INVISIBLE_FLAG), A ; OVERRIDDEN IF GHOST FLASHING STARTS ON SAME FRAME
    RET
@ghostKill:
;   GHOST IS KILLING PAC-MAN
    ; SWITCH TO FIRST DEAD MODE
    LD HL, $01 * $100 + GAMEPLAY_DEAD00
    LD (subGameMode), HL
    RET


/*
    INFO: CHECKS IF PAC-MAN INTERSECTS WITH ANY OF THE 4 GHOSTS (USING PIXEL POSITIONS)
    INPUT: NONE
    OUTPUT: NONE
    USES:  AF, BC, DE, HL, IX
*/
globalCollCheckPixel:
;   CHECK IF A COLLISION WAS DETECTED BY THE 1ST COLLISION FUNCTION
    LD A, (ghostPointSprNum)
    OR A
    RET NZ  ; IF SO, EXIT
;   CHECK IF GAME MODE IS SUPER
    LD A, (pacPoweredUp)
    OR A
    RET Z   ; IF NOT, EXIT
;   SETUP
    LD E, $04       ; COLLISION TOLERANCE
    LD IX, clyde
    LD BC, -_sizeof_ghost
    LD IY, pacman
;   CLYDE
    ; CHECK IF GHOST IS ALIVE
    LD A, (clyde + ALIVE_FLAG)
    OR A
    JR Z, +     ; IF NOT, CHECK NEXT GHOST
    ; CHECK IF GHOST IS WITHIN TOLERANCE OF PAC-MAN'S POSITION
    LD A, (clyde + X_WHOLE)
    SUB A, (IY + X_WHOLE)
    CP A, E
    JR NC, +    ; IF NOT, CHECK NEXT GHOST
    LD A, (clyde + Y_WHOLE)
    SUB A, (IY + Y_WHOLE)
    CP A, E
    JR C, interactDetermination ; IF SO, CHECK GHOST'S STATE TO DETERMINE NEXT ACTION
;   INKY
+:
    ADD IX, BC  ; GHOST POINTER = INKY
    ; CHECK IF GHOST IS ALIVE
    LD A, (inky + ALIVE_FLAG)
    OR A
    JR Z, +     ; IF NOT, CHECK NEXT GHOST
    ; CHECK IF GHOST IS WITHIN TOLERANCE OF PAC-MAN'S POSITION
    LD A, (inky + X_WHOLE)
    SUB A, (IY + X_WHOLE)
    CP A, E
    JR NC, +    ; IF NOT, CHECK NEXT GHOST
    LD A, (inky + Y_WHOLE)
    SUB A, (IY + Y_WHOLE)
    CP A, E
    JR C, interactDetermination ; IF SO, CHECK GHOST'S STATE TO DETERMINE NEXT ACTION
;   PINKY
+:
    ADD IX, BC  ; GHOST POINTER = PINKY
    ; CHECK IF GHOST IS ALIVE
    LD A, (pinky + ALIVE_FLAG)
    OR A
    JR Z, +     ; IF NOT, CHECK NEXT GHOST
    ; CHECK IF GHOST IS WITHIN TOLERANCE OF PAC-MAN'S POSITION
    LD A, (pinky + X_WHOLE)
    SUB A, (IY + X_WHOLE)
    CP A, E
    JR NC, +    ; IF NOT, CHECK NEXT GHOST
    LD A, (pinky + Y_WHOLE)
    SUB A, (IY + Y_WHOLE)
    CP A, E
    JP C, interactDetermination ; IF SO, CHECK GHOST'S STATE TO DETERMINE NEXT ACTION
;   BLINKY
+:
    ADD IX, BC  ; GHOST POINTER = BLINKY
    ; CHECK IF GHOST IS ALIVE
    LD A, (blinky + ALIVE_FLAG)
    OR A
    RET Z   ; IF NOT, END
    ; CHECK IF GHOST IS WITHIN TOLERANCE OF PAC-MAN'S POSITION
    LD A, (blinky + X_WHOLE)
    SUB A, (IY + X_WHOLE)
    CP A, E
    RET NC  ; IF NOT, END
    LD A, (blinky + Y_WHOLE)
    SUB A, (IY + Y_WHOLE)
    CP A, E
    JP C, interactDetermination ; IF SO, CHECK GHOST'S STATE TO DETERMINE NEXT ACTION
    RET