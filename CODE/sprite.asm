/*
    INFO: DISPLAYS A 1 TILE SPRITE ON SCREEN
    INPUT: HL - ADDRESS OF TILE INDEX LIST, D - X POS, E - Y POS, A - SPRITE TABLE INDEX
    OUTPUT: NONE
    USES: AF, BC, DE, HL
*/
display1TileSprite:
;   SETUP HIGH BYTE OF VDP ADDRESS AND PORT
    LD BC, (hibyte(VRAMWRITE) | hibyte(SPRITE_TABLE)) * $100 + VDPCON_PORT
;   SET VDP ADDRESS FOR Y VALUES
    OUT (VDPCON_PORT), A
    OUT (C), B
    DEC C   ; DATA PORT
;   PREPARE Y VALUES
    DEC E   ; VDP DISPLAYS SPRITES AT (Y + 1), SO REMOVE 1 SO IT DISPLAYS CORRECTLY
;   SINGLE TILE SPRITE
    OUT (C), E
;   OTHER SPRITE TILE (HIDDEN)
    LD E, $C0
    OUT (C), E   ; SET Y TO 0
;   OTHER SPRITE TILE (HIDDEN)
    OUT (C), E   ; SET Y TO 0
;   OTHER SPRITE TILE (HIDDEN)
    OUT (C), E   ; SET Y TO 0
;   SET VDP ADDRESS FOR X AND INDEX VALUES
    ADD A, A
    ADD A, $80
    OUT (VDPCON_PORT), A
    INC C   ; CONTROL PORT
    OUT (C), B
    DEC C   ; DATA PORT
;   SPRITE TILE
    OUT (C), D    ; SET X
    OUTI
;   OTHER SPRITE TILE (HIDDEN)
    XOR A
    OUT (VDPDATA_PORT), A    ; SET X TO 0
    OUT (VDPDATA_PORT), A    ; SET INDEX TO 0
;   OTHER SPRITE TILE (HIDDEN)
    OUT (VDPDATA_PORT), A    ; SET X TO 0
    OUT (VDPDATA_PORT), A    ; SET INDEX TO 0
;   OTHER SPRITE TILE (HIDDEN)
    OUT (VDPDATA_PORT), A    ; SET X TO 0
    OUT (VDPDATA_PORT), A    ; SET INDEX TO 0
    RET



/*
    INFO: DISPLAYS A 2 TILE (HORIZONTALLY) SPRITE ON SCREEN
    INPUT: HL - ADDRESS OF TILE INDEX LIST, D - X POS, E - Y POS, A - SPRITE TABLE INDEX
    OUTPUT: NONE
    USES: AF, BC, DE, HL

    ORDER: 1 2
*/
display2HTileSprite:
;   SETUP HIGH BYTE OF VDP ADDRESS AND PORT
    LD BC, (hibyte(VRAMWRITE) | hibyte(SPRITE_TABLE)) * $100 + VDPCON_PORT
;   SET VDP ADDRESS FOR Y VALUES
    OUT (VDPCON_PORT), A
    OUT (C), B
    DEC C   ; DATA PORT
;   PREPARE Y VALUES
    DEC E   ; VDP DISPLAYS SPRITES AT (Y + 1), SO REMOVE 1 SO IT DISPLAYS CORRECTLY
;   LEFT SPRITE TILE
    OUT (C), E
;   RIGHT SPRITE TILE
    OUT (C), E
;   OTHER SPRITE TILE (HIDDEN)
    LD E, $BF + 8
    OUT (C), E    ; SET Y TO 0
;   OTHER SPRITE TILE (HIDDEN)
    OUT (C), E    ; SET Y TO 0
;   SET VDP ADDRESS FOR X AND INDEX VALUES
    ADD A, A
    ADD A, $80
    OUT (VDPCON_PORT), A
    INC C   ; CONTROL PORT
    OUT (C), B
    DEC C   ; DATA PORT
;   LEFT SPRITE TILE
    OUT (C), D  ; SET X
    OUTI        ; SET INDEX
;   RIGHT SPRITE TILE
    LD A, D
    ADD A, $08
    OUT (VDPDATA_PORT), A    ; SET X
    OUTI                     ; SET INDEX
;   OTHER SPRITE TILE (HIDDEN)
    XOR A
    OUT (VDPDATA_PORT), A    ; SET X TO 0
    OUT (VDPDATA_PORT), A    ; SET INDEX TO 0
;   OTHER SPRITE TILE (HIDDEN)
    OUT (VDPDATA_PORT), A    ; SET X TO 0
    OUT (VDPDATA_PORT), A    ; SET INDEX TO 0
    RET



/*
    INFO: DISPLAYS A 2 TILE (VERTICALLY) SPRITE ON SCREEN
    INPUT: HL - ADDRESS OF TILE INDEX LIST, D - X POS, E - Y POS, A - SPRITE TABLE INDEX
    OUTPUT: NONE
    USES: AF, BC, DE, HL, AF'

    ORDER: 1
           2
*/
display2VTileSprite:
;   SETUP HIGH BYTE OF VDP ADDRESS AND PORT
    LD BC, (hibyte(VRAMWRITE) | hibyte(SPRITE_TABLE)) * $100 + VDPCON_PORT
;   SET VDP ADDRESS FOR Y VALUES
    OUT (VDPCON_PORT), A
    OUT (C), B
    DEC C   ; DATA PORT
;   PREPARE Y VALUES
    DEC E   ; VDP DISPLAYS SPRITES AT (Y + 1), SO REMOVE 1 SO IT DISPLAYS CORRECTLY
    EX AF, AF'  ; SAVE SPRITE NUMBER
    LD A, $08
    ADD A, E
;   TOP SPRITE TILE
    OUT (C), E
;   BOTTOM SPRITE TILE
    OUT (VDPDATA_PORT), A
;   OTHER SPRITE TILE (HIDDEN)  
    LD E, $BF + 8
    OUT (C), E      ; SET Y TO 0
;   OTHER SPRITE TILE (HIDDEN)
    OUT (C), E      ; SET Y TO 0
;   SET VDP ADDRESS FOR X AND INDEX VALUES
    EX AF, AF'  ; RESTORE
    ADD A, A
    ADD A, $80
    OUT (VDPCON_PORT), A
    INC C   ; CONTROL PORT
    OUT (C), B
    DEC C   ; DATA PORT
;   TOP SPRITE TILE
    OUT (C), D  ; SET X
    OUTI        ; SET INDEX
;   BOTTOM SPRITE TILE
    OUT (C), D  ; SET X
    OUTI        ; SET INDEX
;   OTHER SPRITE TILE (HIDDEN)
    XOR A
    OUT (VDPDATA_PORT), A    ; SET X TO 0
    OUT (VDPDATA_PORT), A    ; SET INDEX TO 0
;   OTHER SPRITE TILE (HIDDEN)
    OUT (VDPDATA_PORT), A    ; SET X TO 0
    OUT (VDPDATA_PORT), A    ; SET INDEX TO 0
    RET



/*
    INFO: DISPLAYS A 4 TILE SPRITE ON SCREEN
    INPUT: HL - ADDRESS OF TILE INDEX LIST, D - X POS, E - Y POS, A - SPRITE TABLE INDEX
    OUTPUT: NONE
    USES: AF, BC, DE, HL, AF'

    ORDER: 1 3
           2 4
*/
display4TileSprite:
;   SETUP HIGH BYTE OF VDP ADDRESS AND PORT
    LD BC, (hibyte(VRAMWRITE) | hibyte(SPRITE_TABLE)) * $100 + VDPCON_PORT
;   SET VDP ADDRESS FOR Y VALUES
    OUT (VDPCON_PORT), A
    OUT (C), B
    DEC C       ; DATA PORT
;   PREPARE Y VALUES
    DEC E       ; VDP DISPLAYS SPRITES AT (Y + 1), SO REMOVE 1 SO IT DISPLAYS CORRECTLY
    EX AF, AF'  ; SAVE SPRITE NUMBER
    LD A, $08
    ADD A, E
;   TOP-LEFT SPRITE TILE
    OUT (C), E
;   BOTTOM-LEFT SPRITE TILE
    OUT (VDPDATA_PORT), A
;   TOP-RIGHT SPRITE TILE
    OUT (C), E
;   BOTTOM-RIGHT SPRITE TILE
    OUT (VDPDATA_PORT), A
;   SET VDP ADDRESS FOR X AND INDEX VALUES
    EX AF, AF'  ; RESTORE
    ADD A, A
    ADD A, $80
    OUT (VDPCON_PORT), A
    INC C       ; CONTROL PORT
    OUT (C), B
    DEC C       ; DATA PORT
;   TOP-LEFT SPRITE TILE
    OUT (C), D    ; SET X
    OUTI
;   BOTTOM-LEFT SPRITE TILE
    OUT (C), D    ; SET X
    OUTI
;   PREPARE EXTRA X VALUE
    LD A, $08
    ADD A, D
;   TOP-RIGHT SPRITE TILE
    OUT (VDPDATA_PORT), A    ; SET X
    OUTI
;   BOTTOM-RIGHT SPRITE TILE
    OUT (VDPDATA_PORT), A    ; SET X
    OUTI    
    RET




/*
    INFO: DISPLAYS A 9 TILE SPRITE ON SCREEN
    INPUT: HL - ADDRESS OF TILE INDEX LIST, D - X POS, E - Y POS, A - SPRITE TABLE INDEX
    OUTPUT: NONE
    USES: AF, HL, DE, C, I

    ORDER: 1 4 7
           2 5 8
           3 6 9
*/
display9TileSprite:
    LD I, A
    LD C, VDPDATA_PORT
;   SET VDP ADDRESS FOR X AND INDEX VALUES
    ADD A, A
    ADD A, $80
    OUT (VDPCON_PORT), A
    LD A, hibyte(VRAMWRITE) | hibyte(SPRITE_TABLE)
    OUT (VDPCON_PORT), A
;   TOP-LEFT SPRITE TILE
    OUT (C), D    ; SET X
    OUTI
;   MIDDLE-LEFT SPRITE TILE
    OUT (C), D    ; SET X
    OUTI
;   BOTTOM-LEFT SPRITE TILE
    OUT (C), D    ; SET X
    OUTI
;   PREPARE EXTRA X VALUE
    LD A, $08
    ADD A, D
;   TOP-MIDDLE SPRITE TILE
    OUT (VDPDATA_PORT), A    ; SET X
    OUTI
;   MIDDLE-MIDDLE SPRITE TILE
    OUT (VDPDATA_PORT), A    ; SET X
    OUTI
;   BOTTOM-MIDDLE SPRITE TILE
    OUT (VDPDATA_PORT), A    ; SET X
    OUTI
;   PREPARE EXTRA X VALUE
    LD A, $10
    ADD A, D
;   TOP-RIGHT SPRITE TILE
    OUT (VDPDATA_PORT), A    ; SET X
    OUTI
;   MIDDLE-RIGHT SPRITE TILE
    OUT (VDPDATA_PORT), A    ; SET X
    OUTI
;   BOTTOM-RIGHT SPRITE TILE
    OUT (VDPDATA_PORT), A    ; SET X
    OUTI
;   SET VDP ADDRESS FOR Y VALUES
    LD A, I
    OUT (VDPCON_PORT), A
    LD A, hibyte(VRAMWRITE) | hibyte(SPRITE_TABLE)
    OUT (VDPCON_PORT), A
;   PREPARE Y VALUES
    DEC E   ; VDP DISPLAYS SPRITES AT (Y + 1), SO REMOVE 1 SO IT DISPLAYS CORRECTLY
    LD A, $08
    ADD A, E
    LD D, A ; D - MIDDLE Y
    LD A, $10
    ADD A, E ; A - BOTTOM Y
;   TOP-LEFT SPRITE TILE
    OUT (C), E
;   MIDDLE-LEFT SPRITE TILE
    OUT (C), D
;   BOTTOM-LEFT SPRITE TILE
    OUT (VDPDATA_PORT), A
;   TOP-MIDDLE SPRITE TILE
    OUT (C), E
;   MIDDLE-MIDDLE SPRITE TILE
    OUT (C), D
;   BOTTOM-MIDDLE SPRITE TILE
    OUT (VDPDATA_PORT), A
;   TOP-RIGHT SPRITE TILE
    OUT (C), E
;   MIDDLE-RIGHT SPRITE TILE
    OUT (C), D
;   BOTTOM-RIGHT SPRITE TILE
    OUT (VDPDATA_PORT), A
    RET